//お勉強用にミドナのルーティンを弄ってみたやつ　テストは完全ではないので実用時は適当に弄って下さい。

event getIndexNumber
{
	//SAVEalltalentには全人材をsetudしておく
	//この変数の格納順と同じ順番に要素を並べることにより、個人データは全員後述の
	//共通ルーティンで取り出せる
	//getIndexNumberはindexNumberにindexで何番を入力すれば取り出せるかを返す
	storeud(SAVEalltalent,@alltalent)
	pushv(@alltalent,idx)
	set(indexNumber,0)
	while (indexNumber<idx) {
		index(@alltalent,indexNumber,@talent)
		if (@talent==@unit) {break()}
		add(indexNumber,1)
	}
}

event privateData
{
	//個別の識別子や数値変数に保存していたデータを全ユニット共通にすることにより、簡易なルーティンに
	//よるゲーム中の書き換えがしやすくなり、メンテナンス性も上がります…動けば。
	//if式数は総合的に見ればさほど変化はないと思います。
	//あとはstoreud、setud、indexあたりの処理速度がどの程度であるか次第か。
	//ちょっと動かしてみた感じだとそんなに重くは無かったです。

	routine(getIndexNumber)

	if (purpose == 1) {
		//所属勢力の保存
		//使用ルーティンはスクリプト最後に
		storeud(SAVEbelongingToPower,@routineSends)
		setv(@routineSends2,@power)
		routine(changePrivateData)
		setud(SAVEbelongingToPower,@routineResults)
	}else if (purpose == 2) {
		//保存した所属勢力の取り出し
		storeud(SAVEbelongingToPower,@belongingToPower)
		index(@belongingToPower,indexNumber,@belongingToPower)
	}else if (purpose == 3) {
		//メンバー数の保存 数値を文字変数に変換して保存する
		//使用ルーティンはスクリプト最後に
		storeud(SAVEnumberOfMember,@routineSends)

		set(routineSend,numberOfMember)
		routine(numberTranslate)
		setv(@routineSends2,@routineResults)

		routine(changePrivateData)
		setud(SAVEnumberOfMember,@routineResults)
	}else if (purpose == 4) {
		//メンバー数の取り出し 文字変数を数値に変換して返す
		//使用ルーティンはスクリプト最後に
		storeud(SAVEnumberOfMember,@routineSends)
		index(@routineSends,indexNumber,@routineSends)
		routine(numberTranslate2)
		set(numberOfMember,routineResult)
	}else if (purpose == 5) {
		//指向クラスの取り出し
		storeud(SAVEobjectiveClass,@objectiveClass)
		index(@objectiveClass,indexNumber,@objectiveClass)

		//objectiveNumberの取り出し　文字変数を数値に変換して返す
		storeud(SAVEobjectiveNumber,@routineSends)
		index(@routineSends,indexNumber,@routineSends)
		routine(numberTranslate2)
		set(objectiveNumber,routineResult)
	}else if (purpose == 6) {
		//内政ユニットの取り出し
		storeud(SAVEpoliticsUnit,@politicsUnit)
		index(@politicsUnit,indexNumber,@politicsUnit)
	}else if (purpose == 7) {
		//開始クラスの取り出し
		storeud(SAVEstartClass,@startClass)
		index(@startClass,indexNumber,@startClass)

		//開始レベルの取り出し 文字変数を数値に変換して返す
		storeud(SAVEstartLv,@routineSends)
		index(@routineSends,indexNumber,@routineSends)
		routine(numberTranslate2)
		set(startLv,routineResult)
	}
}